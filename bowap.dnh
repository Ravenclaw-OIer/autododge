#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Modeling 2"]
#Text["Border of Wave and Particle"]
#include "script/default_system/Default_ShotConst.txt"
#include "./lib/general.dnh"
#include "./player-logic.dnh"
let objBoss;
let frame = 0;
let objScene = GetEnemyBossSceneObjectID();

@Event{
  alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
      SetScriptResult(500);
    }
    case (EV_REQUEST_TIMER) {
      SetScriptResult(60);
    }
    case (EV_REQUEST_SPELL_SCORE) {
      SetScriptResult(1000000);
    }

}


@Initialize{
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);
  ObjEnemyBossScene_StartSpell(objScene);
  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY(), 60);
  //logger = ObjLogger_Create("./log.txt");
  TDrawLoop;
  TFinalize;
}

task TDrawLoop {
  let imgExRumia = GetCurrentScriptDirectory() ~ "ExRumia.png";
  ObjPrim_SetTexture(objBoss, imgExRumia);
  ObjSprite2D_SetSourceRect(objBoss, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objBoss);
}

@MainLoop{
ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
  if (frame == 61) { // We have finished moving
    BoWaP;
  }
  frame++;
  TPlayerLogicMain;
  yield;
}
task BoWaP{
    let angleT = rand(0, 360);
    let objcount = 0;
    loop {
        loop(5){
            let obj = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 3, angleT, 47, 5);
            angleT += 360/5;
        }
        angleT += sin(objcount) * cos(objcount) * 12; //BOWAP
        objcount++;
        yield;
    }
}

task TFinalize {
  while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) yield;
  //Obj_Delete(logger);
  Obj_Delete(objBoss);
  DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
  SetAutoDeleteObject(true);
  CloseScript(GetOwnScriptID());
  return;
}
